%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%    Victor V. Matveev, Richard Bertram and Arthur Sherman (2009)
%    Ca2+ current vs. Ca2+ channel cooperativity of exocytosis
%        Journal of Neuroscience, 29(39): 12196-12209. 
%
%  This CalC simulation script reproduces Fig. 1 of the manuscript 
%               Victor Matveev, January 8, 2010
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

verbose = 0

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

B.total = $2          % Single command-line parameter: total buffer concentration (uM)
Dist = 0.03 / sqrt(2) % Distance from calcium channel to sensor is 30 nm (0.03 um)

%%%% Create file names for data output (file) and binding gate initialization (ICfile) 

file = $1 "_Btot" $2 "_Dist30nm_" N "x" N "x" M ".dat"
ICfile = file ".IC"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

L = 1
volume 0 L 0 L 0 L

N = 50
M = 40
grid N N M
stretch z 0 0
stretch x 0 Dist
stretch y 0 Dist
stretch.factor = 1.05

Ca.source 0 0 0 ww ww ww
ww = 0.005

Ca.D = 0.2
Ca.bgr = 0.1

Run adaptive T1
current = ICA pA

Run adaptive T2
current = 0 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

buffer B
B.D = 0.05
B.KD = 1
B.kplus = 0.65

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

uptake = 0.004

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Ca2+ binding scheme: Felmy Neher Schneggenburger (2003)
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

C := Ca[Dist,Dist,0]  % Release is drive by [Ca2+] at 30nm from channel mouth

dX/dt  =                             - 5 C kon X  +   koff     X1
dX1/dt =  5 kon C X  -   koff     X1 - 4 C kon X1 + 2 koff b   X2
dX2/dt =  4 kon C X1 - 2 koff b   X2 - 3 C kon X2 + 3 koff b^2 X3
dX3/dt =  3 kon C X2 - 3 koff b^2 X3 - 2 C kon X3 + 4 koff b^3 X4
dX4/dt =  2 kon C X3 - 4 koff b^3 X4 -   C kon X4 + 5 koff b^4 X5
dX4/dt =  2 kon C X3 - 4 koff b^3 X4 -   C kon X4 + 5 koff b^4 X5
dX5/dt =                                 C kon X4 - 5 koff b^4 X5 - gamma X5

R := gamma X5   % Release rate

kon = 0.116     % Binding scheme rate constants
koff = 8.430
gamma = 6.96  
b = 0.25        % cooperativity parameter

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

T1 = 1        % Duration of current pulse (ms)
T2 = 4        % "Silent" interval after current pulse (ms)
TM = T1 + T2  % Total duration of sumulation

Tmax Rmax max R 0 TM       % Rmax = max release; Tmax = time of max release
Cmax      max C 0 TM       % Cmax = max of [Ca2+] at select distance from channel
CAmax     max Ca[] 0 TM    % CAmax = max volume-averaged [Ca2+]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

ICA1 = 0.001       % minimal single-channel Ca2+ current
ICA2 = 1           % maximal single-channel Ca2+ current
steps = 40         % steps in the for-loop over Ca2+ current values

Log1 = log(ICA1)
Log2 = log(ICA2)
dlog = (Log2 - Log1) / steps

for Istep = -1 to steps step 1   %%%%%%% MAIN FOR-LOOP STATEMENT %%%%%%%

if Istep == -1 then
  ICA = 0
else
  ICA = exp(Log1 + Istep * dlog)
endif

if Istep == -1     % first iteration in the loop (step=-1): zero-current pre-pulse of 12 ms
    T2 = 12
    X(0) = 1
    print  ICfile "X(0)="  X    % Store the gate binding states 12 ms after simulation start
    append ICfile "X1(0)=" X1
    append ICfile "X2(0)=" X2
    append ICfile "X3(0)=" X3
    append ICfile "X4(0)=" X4
    append ICfile "Rmax0=" Rmax "; Tmax0=" Tmax "; Cmax0=" Cmax
    print file
else
    include ICfile
endif

%%%%% Data output: four columns [ ICa Rmax Cmax CAmax Tmax ]

parStr = ICA
append file parStr "    " Rmax " " Cmax " " CAmax " " Tmax

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
